buildscript {
    ext {
        androidGradlePluginVersion = '8.5.2'
        kotlinVersion = '2.0.0'
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:$androidGradlePluginVersion")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
    }
}

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'maven-publish'

def safeExtGet(prop, fallback) {
    return rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

group = 'com.hawcx.reactnative'
version = project.findProperty('HAWCX_REACT_NATIVE_ANDROID_VERSION') ?: '1.0.5'

repositories {
    mavenCentral()
    google()
    maven { url "$rootDir/../node_modules/react-native/android" }
    maven { url "$rootDir/../node_modules/jsc-android/dist" }
    maven {
        url "https://raw.githubusercontent.com/hawcx/hawcx_android_sdk/main/maven"
        credentials {
            username System.getenv("HAWCX_MAVEN_USER") ?: System.getenv("GITHUB_USER") ?: ""
            password System.getenv("HAWCX_MAVEN_TOKEN") ?: System.getenv("GITHUB_TOKEN") ?: ""
        }
        authentication {
            basic(BasicAuthentication)
        }
        content {
            includeGroup "api.hawcx"
        }
        metadataSources {
            mavenPom()
            artifact()
        }
    }
    def localMavenRepo = new File(rootDir, "../../prod_android/hawcx_android_sdk/maven")
    if (localMavenRepo.exists()) {
        maven {
            url localMavenRepo.toURI()
            content {
                includeGroup "api.hawcx"
            }
            metadataSources {
                mavenPom()
                artifact()
            }
        }
    }
}

android {
    namespace 'com.hawcx.reactnative'
    compileSdkVersion safeExtGet('compileSdkVersion', 34)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 26)
        targetSdkVersion safeExtGet('targetSdkVersion', 34)
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    lint {
        abortOnError = true
        textReport = true
        textOutput = file("${project.buildDir}/reports/lint-results.txt")
        htmlReport = true
        htmlOutput = file("${project.buildDir}/reports/lint-results.html")
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.hawcx'
                artifactId = 'react-native-hawcx'
                version = project.version
                pom {
                    name = 'Hawcx React Native SDK - Android'
                    description = 'React Native bridge for the Hawcx Android SDK.'
                    url = 'https://github.com/hawcx/react-native-sdk'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    api("api.hawcx:hawcx:1.0.5")
    def reactNativeVersion = safeExtGet('reactNativeAndroidVersion', '0.73.9')
    implementation "com.facebook.react:react-android:$reactNativeVersion"
    implementation 'androidx.annotation:annotation:1.8.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.biometric:biometric-ktx:1.2.0-alpha05'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'com.squareup.retrofit2:retrofit:2.10.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.10.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.11.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.google.android.gms:play-services-tasks:18.2.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'org.bouncycastle:bcprov-jdk15to18:1.78.1'
}
